document = {
	doctype? ~ element
}
element = {
	WS* ~ NL* ~ ( comment | tag | text )+ ~ WS* ~ NL* |
	WS+ |
	NL+
}

WS = _{ " " | "\t" }
NL = _{ "\n" | "\r" }

doctype = {
	"<!DOCTYPE" ~ WS* ~ doctype_type ~ ">"
}

doctype_type = {
	(!(">" | NL) ~ ANY)*
}

comment = {
	"<!--" ~ comment_content ~ "-->"
}

comment_content = {
	(!("-->") ~ ANY)*
}

tag = {
	insides |
	single
}

single = {
	"<" ~ tagname ~ attr* ~ WS* ~ "/"? ~ ">"
}

tagname = @{
	(!("<" | ">" | "/" | "!" | WS) ~ ASCII)+
}

text = {
	(!("<" | ">") ~ ANY)+
}

attr = {
	WS* ~ attrname ~ "=" ~ "\"" ~ attrval ~ "\""
}

attrname = {
	(!("\"" | "<" | ">" | WS | NL) ~ ASCII_ALPHA)+
}

attrval = {
	(!("\"" | "<" | ">" | NL) ~ ANY)*
}

closetag = {
	"</" ~ tagname ~ ">"
}

opentag = {
	"<" ~ tagname ~ attr* ~ WS* ~ ">"
}

insides = {
	opentag ~ element* ~ closetag
}